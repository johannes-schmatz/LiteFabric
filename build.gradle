import java.text.SimpleDateFormat

plugins {
	id 'org.quiltmc.loom' version '0.12.15'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group = name
archivesBaseName = name

def buildDate = new Date()
if (project.mod_version.endsWith('-dev')) {
	def df = new SimpleDateFormat(".yyyyMMdd.HHmmss")
	df.setTimeZone(TimeZone.getTimeZone("UTC"))
	project.mod_version += df.format(buildDate)
}
version = project.mod_version

repositories {
	mavenLocal()
	maven {
		name = 'Fabric'
		url = 'https://maven.fabricmc.net/'
	}
	maven {
		name = 'legacy-fabric'
		url = 'https://maven.legacyfabric.net/'
	}
}

loom {
	setIntermediaryUrl('https://maven.legacyfabric.net/net/fabricmc/intermediary/%1$s/intermediary-%1$s-v2.jar')
	customMinecraftManifest.set("https://meta.legacyfabric.net/v2/manifest/${minecraft_version}")
}

dependencies {
	implementation "com.google.guava:guava:23.5-jre"
	implementation("com.google.jimfs:jimfs:1.2") {
		exclude module: "guava"
	}
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "de.skyrising:modmenu:1.16.9+1.12.2.e6bcc00"
}


processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

task generateJava(type: Copy) {
	group = 'build'
	description 'Generates Build.java'

	def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss'Z'")
	df.setTimeZone(TimeZone.getTimeZone("UTC"))
	def templateContext = [
			version: project.version,
			timestamp: df.format(buildDate),
			branch: GitHelper.branch,
			commit: GitHelper.commit,
			working_dir_clean: GitHelper.status,
			minecraft_version: project.minecraft_version,
			yarn_mappings: project.yarn_mappings
	]
	inputs.properties templateContext // for gradle up-to-date check
	from 'src/template/java'
	into "$buildDir/generated/java"
	expand templateContext
}

sourceSets.main.java.srcDir "$buildDir/generated/java"
compileJava.dependsOn generateJava

apply from: 'setupAuth.gradle'