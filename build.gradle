import java.text.SimpleDateFormat

plugins {
	id 'fabric-loom' version '0.6-SNAPSHOT'
}

group = name
archivesBaseName = name

def buildDate = new Date()
if (project.mod_version.endsWith('-dev')) {
	def df = new SimpleDateFormat(".yyyyMMdd.HHmmss")
	df.setTimeZone(TimeZone.getTimeZone("UTC"))
	project.mod_version += df.format(buildDate)
}
version = project.mod_version

repositories {
	maven {
		name = 'Fabric'
		url = 'http://maven.fabricmc.net/'
	}
	maven {
		name = 'legacy-fabric'
		url = 'https://dl.bintray.com/legacy-fabric/Legacy-Fabric-Maven'
	}
	maven {
		name = 'Jitpack'
		url = 'https://jitpack.io/'
	}
}

minecraft {
	intermediaryUrl = { version ->
		return "https://dl.bintray.com/legacy-fabric/Legacy-Fabric-Maven/net/fabricmc/intermediary/${version}/intermediary-${version}-v2.jar";
	}
}

dependencies {
	implementation "com.google.guava:guava:23.5-jre"
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
}

processResources {
	def properties = [
			version: project.version
	]
	inputs.properties properties

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand properties
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

task generateJava(type: Copy) {
	group = 'build'
	description 'Generates Build.java'

	def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss'Z'")
	df.setTimeZone(TimeZone.getTimeZone("UTC"))
	def templateContext = [
			version: project.version,
			timestamp: df.format(buildDate),
			branch: GitHelper.branch,
			commit: GitHelper.commit,
			working_dir_clean: GitHelper.status,
			minecraft_version: project.minecraft_version,
			yarn_mappings: project.yarn_mappings
	]
	inputs.properties templateContext // for gradle up-to-date check
	from 'src/template/java'
	into "$buildDir/generated/java"
	expand templateContext
}

sourceSets.main.java.srcDir "$buildDir/generated/java"
compileJava.dependsOn generateJava

apply from: 'setupAuth.gradle'